apply plugin: 'com.android.library'
apply from: '../gradle/publish.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    libraryVariants.all { variant ->
        variant.outputs.all {
            def fileName = "${archivesBaseName}-${version}.aar"
            outputFileName = fileName
        }
        task("generate${variant.name}Javadoc", type: Javadoc) {
//        task("generateJavadoc", type: Javadoc) {
            title = "$name $version API"
            source = variant.javaCompile.source
            ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            doFirst {
                classpath = files(variant.javaCompile.classpath.files, ext.androidJar)
            }
            options {
                links("http://docs.oracle.com/javase/11/docs/api/");
                linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference");
                encoding "UTF-8"
                charSet 'UTF-8'
                author true
                version true
                splitIndex true
                noDeprecated true
                setMemberLevel(JavadocMemberLevel.PUBLIC)
            }
            failOnError false
//            exclude '**/BuildConfig.java'
//            exclude '**/R.java'
//            destinationDir = reporting.file("$project.rootDir/docs/")
            destinationDir = reporting.file("$project.buildDir/outputs/jar/javadoc/")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
